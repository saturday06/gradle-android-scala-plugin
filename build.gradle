/*
 * Copyright 2014 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'idea'

repositories {
    mavenCentral()
    maven { url "http://repository-dex2jar.forge.cloudbees.com/release/" }
}

buildscript {
    repositories {
        mavenCentral()
    }
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    testCompile "com.android.tools.build:gradle:0.9.0"
    testCompile "com.google.guava:guava:16.0.1"
    testCompile "com.googlecode.dex2jar:dex-tools:0.0.9.15@zip"
    testCompile "net.sf.proguard:proguard-anttask:4.11"
    testCompile "org.scala-lang:scala-compiler:2.10.3"
    testCompile "org.mockito:mockito-core:1.9.5"
}

description 'Gradle Android Scala Plugin adds scala language support to official gradle android plugin.'
group = 'jp.leafytree.gradle'
version = '1.0-SNAPSHOT'

def uploadToSonatypeRepository = hasProperty('sonatypeUserName')

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: uri('gh-pages/repository/snapshot'))
        }
        if (uploadToSonatypeRepository) {
            def repositoryUrl = version.endsWith("-SNAPSHOT") ? "https://oss.sonatype.org/content/repositories/snapshots"
                                                              : "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: repositoryUrl) {
                    authentication(userName: sonatypeUserName, password: sonatypePassword)
                }
                pom.project {
                    name 'Gradle Android Scala Plugin'
                    packaging 'jar'
                    description project.description
                    url 'https://github.com/saturday06/gradle-android-scala-plugin'

                    scm {
                        url 'scm:git@github.com:saturday06/gradle-android-scala-plugin.git'
                        connection 'scm:git@github.com:saturday06/gradle-android-scala-plugin.git'
                        developerConnection 'scm:git@github.com:saturday06/gradle-android-scala-plugin.git'
                    }

                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'saturday06'
                            name 'Isamu Mogi'
                        }
                    }
                }
            }
        }
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourceJar
}

signing {
    if (uploadToSonatypeRepository) {
        sign configurations.archives
    }
}

task integTest(type: jp.leafytree.gradle.AndroidScalaPluginIntegrationTestTask)
task integrationTest(type: jp.leafytree.gradle.AndroidScalaPluginIntegrationTestTask)
task sample(type: jp.leafytree.gradle.AndroidScalaPluginSampleTask)
